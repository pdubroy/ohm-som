SOM {
/*
    classdef =
        Identifier Equal superclass instanceFields method* ( Separator classFields method* )? endTerm

    superclass =
        Identifier? newTerm

    instanceFields =
        ( or variable* or )?

    classFields =
        ( or variable* or )?
*/

    Method = 
       Pattern equal (primitive | MethodBlock)

    Pattern =
        UnaryPattern | BinaryPattern | KeywordPattern

    UnaryPattern = 
        unarySelector

    BinaryPattern =
        binarySelector argument

    KeywordPattern =
        (keyword argument)+

    MethodBlock (a method block) =
        newTerm BlockContents? endTerm

    unarySelector =
        (primitive | identifier) ~colon

    binarySelector = operatorSequence |
        or | comma | minus | equal | not | and | star | div | mod | plus | more |
        less | at | per

    argument = variable

    BlockContents =
        (or LocalDefs or)? BlockBody

    LocalDefs =
        variable*

    BlockBody =
          exit Result  -- return
        | Expression ( period BlockBody? )? -- rec

    Result =
        Expression period?

    Expression =
        Assignation | Evaluation

    Assignation =
        Assignments Evaluation

    Assignments =
        Assignment+

    Assignment =
        variable assign

    Evaluation =
        Primary Messages?

    Primary =
        variable | NestedTerm | /* nestedBlock | */ Literal

    variable = identifier

    // TODO(pdubroy): Make this recursive instead?
    Messages =
        | UnaryMessage+ BinaryMessage* KeywordMessage?  -- unaryFirst
        | BinaryMessage+ KeywordMessage?  -- binaryFirst
        | KeywordMessage

    UnaryMessage =
        unarySelector

    BinaryMessage =
        binarySelector BinaryOperand

    BinaryOperand =
        Primary UnaryMessage*

    KeywordMessage =
        ( keyword Formula )+

    Formula =
        BinaryOperand BinaryMessage*

    NestedTerm =
        newTerm Expression endTerm

    Literal =
        LiteralArray | LiteralSymbol | LiteralString | LiteralNumber

    LiteralArray =
        pound newTerm Literal* endTerm

    LiteralNumber =
        NegativeDecimal | LiteralDecimal

    LiteralDecimal =
        LiteralDouble | LiteralInteger

    NegativeDecimal =
        minus LiteralDecimal

    LiteralInteger =
        integer

    LiteralDouble =
        double

    LiteralSymbol =
        pound ( string | selector )

    LiteralString =
        string

    selector =
        binarySelector | keywordSelector | unarySelector

    keywordSelector =
        keywordSequence | keyword  // TODO(pdubroy): Remove alternation here, 'keyword' is redundant.

    string =
        stString
/*
    nestedBlock =
        newBlock blockPattern? blockContents? endBlock

    blockPattern =
        blockArguments or

    blockArguments =
        ( Colon argument )+

    // Lexer

    Comment =   '"' ~["]* '"' -> skip
    Whitespace : [ \t\r\n]+ -> skip 
*/
    primitive = "primitive"
    identifier = letter (letter | digit | "_")*

    equal = "="
/*
    Separator = '----' '-'*
*/
    newTerm = "("
    endTerm = ")"
    or = "|"

    comma = ","
    minus = "-"
    not =   "~"
    and =   "&"
    star =  "*"
    div =   "/"
    mod =   "\\"
    plus =  "+"
    more =  ">"
    less =  "<"
    at =    "@"
    per =   "%"

    operatorSequence = (
        not | and | or | star | div |
        mod | plus | equal | more | less |
        comma | at | per | minus )+

    colon = ":"

    newBlock = "["
    endBlock = "]"

    pound = "#"

    exit = "^"
    period = "."
    assign = ":="

    integer = digit+
    double = digit+ "." digit+
    keyword = identifier colon
    keywordSequence = keyword+
    stString =
        "'"
        (   "\\t"
          | "\\b"
          | "\\n"
          | "\\r"
          | "\\f"
          | "\\0"
          | "\\'"
          | "\\\\"
          | ~("'" | "\\") any
        )*
        "'"
}